apply plugin: 'maven'
apply plugin: 'signing'

group 'info.macias'
version '0.8.6'

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

buildscript {
    ext.kotlin_version = '1.2.21'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    if (project.hasProperty('deploy')) {
        sign configurations.archives
    }
}

// Required by uploadArchives
group = "info.macias"
archivesBaseName = "kaconf"
version = "0.8.6"

if (project.hasProperty('deploy')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'KickAss Configuration system'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'An annotation-based configuration system for Java and Kotlin'
                    url 'https://github.com/mariomac/kaconf'

                    scm {
                        connection 'scm:git:git@github.com:mariomac/kaconf.git'
                        developerConnection 'scm:git:git@github.com:mariomac/kaconf.git'
                        url 'git@github.com:mariomac/kaconf.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'mariomac'
                            name 'Mario Macias'
                            email 'mario.macias.upc@gmail.com'
                            url 'http://www.macias.info'
                        }
                    }
                }
            }
        }
    }
}

