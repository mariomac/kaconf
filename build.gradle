buildscript {
    ext.kotlin_version = "1.7.10"
}

plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
    id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
}

group 'info.macias'
version '0.9.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

// Required by uploadArchives
group = "info.macias"
archivesBaseName = "kaconf"
version = "0.9.1"

/* To upload files into sonatype:
export GNUPGHOME=/your/sonatype/.gnupg
./gradlew publishToSonatype closeAndReleaseSonatypeStagingRepository
 */
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'KickAss Configuration system'
                packaging = 'jar'
                // optionally artifactId can be defined here
                description = 'An annotation-based configuration system for Java and Kotlin'
                url = 'https://github.com/mariomac/kaconf'

                scm {
                    connection = 'scm:git:git@github.com:mariomac/kaconf.git'
                    developerConnection = 'scm:git:git@github.com:mariomac/kaconf.git'
                    url = 'git@github.com:mariomac/kaconf.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'mariomac'
                        name = 'Mario Macias'
                        email = 'mario.macias.upc@gmail.com'
                        url = 'http://www.macias.info'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusPublishing {
    repositories {
        sonatype{
            //assigning from the 'gradle.properties' files
            username=project.property("ossrhUsername")
            password=project.property("ossrhPassword")
        }
    }
}
